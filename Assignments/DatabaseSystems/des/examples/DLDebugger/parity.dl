% The parity query. Adapted from [ZCF+97]
% C. Zaniolo, S. Ceri, C. Faloutsos, T.T. Snodgrass, V.S. Subrahmanian, and R. Zicari, 
% "Advanced Database Systems", Morgan Kauffmann Publishers, 1997.
% Reproduced with permission of the author


% Pairs of non-consecutive elements in br
% i(between) = {}
between(X,Z) :-
  br(X), br(Y), br(Z), X<Y, Y<Z.

% Consecutive elements in the sequence, starting at nil
% i(next) = {next(a,b), next(nil,a)}
next(X,Y) :-
  br(X), br(Y), X<Y, not between(X,Y).
next(nil,X) :-
  br(X), not has_preceding(X).

% Values having preceding values in the sequence
% i(has_preceding) = {has_preceding(b)}
has_preceding(X) :- 
  br(X), br(Y), Y>X. % Error: Y>X should be Y<X

% Values in an even position of the sequence, including nil
% i(even) = {even(b), even(nil)}
even(nil).
even(Y) :-
  odd(X), next(X,Y).

% Values in an odd position of the sequence
% i(odd) = {odd(a)}
odd(Y) :-
  even(X), next(X,Y).

% Succeeds if the cardinality of the sequence is even
% i(br_is_even) = {br_is_even}
br_is_even :-
  even(X), not next(X,Y).
  
% i('$p0') = {'$p0'(a),'$p0'(nil)}

% Succeeds if the cardinality of the sequence is odd
% i(br_is_odd) = {}
br_is_odd :-
  odd(X), not next(X,Y).

% i('$p1') = {'$p1'(a),'$p1'(nil)}

% Base relation
% i(br) = {br(a), br(b)}
br(a). % Has no preceding (a previous value X s.t. X>a and X<>nil. nil is the implicit head of the chain). Odd position
br(b). % Has preceding (a>b. So a is a preceding node in the chain). Even position

